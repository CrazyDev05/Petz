plugins {
    id 'java'
    id "com.gradleup.shadow" version "8.3.3"
    id "xyz.jpenilla.run-paper" version "2.3.1"
}

group = 'de.crazydev22'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url "https://repo.crazydev22.de/public"}
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'

    implementation "de.cubbossa:ClientEntities:1.3.7-SNAPSHOT"
    implementation "com.github.retrooper:packetevents-spigot:2.5.0"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier = null

    def libs = "de.crazydev22.petz.libs"
    relocate 'de.cubbossa.cliententities', "${libs}.cliententities"
    relocate 'com.github.retrooper.packetevents', "${libs}.packetevents.api"
    relocate 'io.github.retrooper.packetevents', "${libs}.packetevents.impl"
}

runServer {
    minecraftVersion("1.21.1")
}